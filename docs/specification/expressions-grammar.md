# Грамматика выражений языка Vaibik

## Синтаксис выражений

Выражения в языке `Vaibik` могут содержать:

* Литералы чисел (целые `ЦИФЕРКА` и вещественные `ПОЛТОРАШКА`)
* Логические литералы (`ХАЙП`, `КРИНЖ`)
* Идентификаторы (переменные)
* Арифметические операторы (бинарные и унарные)
* Операторы сравнения
* Логические операторы (`И`, `ИЛИ`, `НЕ`)
* Встроенные константы
* Круглые скобки `()` для группировки
* Вызовы встроенных функций (напр. `abs(x)`)
* При вызове функции `имя(арг1, арг2, ...)` аргументы вычисляются строго слева направо перед передачей в тело функции

## Операторы

### Арифметические операторы

| Символы / Слово | Операция                  | Ассоциативность |
|:----------------|:--------------------------|:----------------|
| `+`             | Сложение или унарный "+"  | Левая           |
| `-`             | Вычитание или унарный "-" | Левая           |
| `*`             | Умножение                 | Левая           |
| `/`             | Вещественное деление      | Левая           |
| `%`             | Остаток от деления        | Левая           |

### Операторы сравнения

| Символы | Операция         |
|:--------|:-----------------|
| `==`    | Равенство        |
| `!=`    | Неравенство      |
| `>`     | Строгое больше   |
| `>=`    | Нестрогое больше |
| `<`     | Строгое меньше   |
| `<=`    | Нестрогое меньше |

### Логические операторы

| Слово | Операция       | Ассоциативность  |
|:------|:---------------|:-----------------|
| `И`   | Логическое И   | Левая            |
| `ИЛИ` | Логическое ИЛИ | Левая            |
| `НЕ`  | Логическое НЕ  | Правая (унарный) |

## Приоритет операторов

| Приоритет (по убыванию) | Операторы                        | Ассоциативность          |
|:------------------------|:---------------------------------|:-------------------------|
| 7 (Высший)              | `( ... )`, `f(...)`              | Группировка, вызов       |
| 6                       | `+`, `-`, `НЕ`                   | Унарные (право)          |
| 5                       | `*`, `/`, `%`,                   | Мультипликативные (лево) |
| 4                       | `+`, `-`                         | Аддитивные (лево)        |
| 3                       | `==`, `!=`, `<`, `>`, `<=`, `>=` | Сравнение (не-ассоц.)    |
| 2                       | `И`                              | Логическое И (лево)      |
| 1                       | `ИЛИ`                            | Логическое ИЛИ (лево)    |

## Встроенные константы

| Константа    | Обозначение | Значение (приблизительное) |
|:-------------|:------------|:---------------------------|
| Число «пи»   | `ПИ`        | `3.1415926535`             |
| Число Эйлера | `ЕШКА`      | `2.7182818284`             |

## Встроенные  функции для чисел

* `МОДУЛЬ(x)` — возвращает модуль (абсолютное значение) числа `x`.
* `МИНИМУМ(x, y, ...)` — возвращает наименьшее из переданных чисел.
* `МАКСИМУМ(x, y, ...)` — возвращает наибольшее из переданных чисел.

## Встроенные тригонометрические функции

* `СИНУС(x)` — возвращает синус угла.
* `КОСИНУС(x)` — возвращает косинус угла.
* `ТАНГЕНС(x)` — возвращает тангенс угла.

## Грамматика в нотации EBNF

### Базовые символы

```
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

underscore = "_" ;

cyrillicLetter = 
    "а" | "б" | "в" | "г" | "д" | "е" | "ё" | "ж" | "з" | "и" | "й" | "к" | "л" | "м" | "н" | "о" | "п" |
    "р" | "с" | "т" | "у" | "ф" | "х" | "ц" | "ч" | "ш" | "щ" | "ъ" | "ы" | "ь" | "э" | "ю" | "я" |
    "А" | "Б" | "В" | "Г" | "Д" | "Е" | "Ё" | "Ж" | "З" | "И" | "Й" | "К" | "Л" | "М" | "Н" | "О" | "П" |
    "Р" | "С" | "Т" | "У" | "Ф" | "Х" | "Ц" | "Ч" | "Ш" | "Щ" | "Ъ" | "Ы" | "Ь" | "Э" | "Ю" | "Я" ;
```

### Лексемы и Синтаксис Инструкций

#### Лексемы (Токены)

```
identifier = ( cyrillicLetter | underscore ), { cyrillicLetter | digit | underscore } ;

numericLiteral = realLiteral | integerLiteral ;
integerLiteral = [ "-" | "+" ], digit, { digit } ;
realLiteral    = [ "-" | "+" ], digit, { digit }, ".", digit, { digit } ;
logicalLiteral = "ХАЙП" | "КРИНЖ" ;

constant = "ПИ" | "ЕШКА" ;

typeName = "ЦИФЕРКА" | "ПОЛТОРАШКА" | "РАСКЛАД" ;
```

#### Грамматика Выражений

```
expression = assignmentExpression ;

assignmentExpression = 
    logicalOrExpression, [ "=", assignmentExpression ] ;

logicalOrExpression = 
    logicalAndExpression, { "ИЛИ", logicalAndExpression } ;

logicalAndExpression = 
    equalityExpression, { "И", equalityExpression } ;

equalityExpression = 
    relationalExpression, { ( "==" | "!=" ), relationalExpression } ;

relationalExpression = 
    additiveExpression, { ( "<" | ">" | "<=" | ">=" ), additiveExpression } ;

additiveExpression = 
    multiplicativeExpression, { ( "+" | "-" ), multiplicativeExpression } ;

multiplicativeExpression =
    unaryExpression, { ( "*" | "/" | "%" ), unaryExpression } ;

unaryExpression = 
      ( "-" | "+" | "НЕ" ), unaryExpression 
    | primary 
    ;

primary = 
      numericLiteral
    | logicalLiteral
    | constant
    | identifier, [ callSuffix ]
    | "(", expression, ")"
    ;

callSuffix = "(", [ argumentList ], ")" ;

argumentList = expression, { ",", expression } ;
```